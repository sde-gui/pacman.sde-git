# -%- indent-width: 4; use-tabs: no; -%-
# Maintainer: Vadim Ushakov <igeekless@gmail.com>

pkgname=sde-randr-git
pkgver=202111300109
pkgrel=1
pkgdesc="A set of scripts and default configuration files for SDE session"
arch=('i686' 'x86_64')
license=('GPL2')
url="https://github.com/sde-gui/sde-randr"
depends=('sde-reverse-meta-git' 'gtk2' 'xorg-xrandr')
makedepends=('git' 'intltool' 'automake')
provides=('sde-randr')
conflicts=('sde-randr')

source=('git+https://github.com/sde-gui/sde-randr.git')
md5sums=('SKIP')

_gitname="sde-randr"

pkgver() {
    date +%Y%m%d%H%M
}

build() {
    if [ -f "$MAKEPKG_CONFIGURE_HOOK" ] ; then
        . "$MAKEPKG_CONFIGURE_HOOK"
    fi

    cd "${_gitname}"

    autoreconf -i
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --enable-man \
        --enable-silent-rules

    if [ -f "$MAKEPKG_BUILD_HOOK" ] ; then
        . "$MAKEPKG_BUILD_HOOK"
    fi

    make
}

package() {
    cd "${_gitname}"
    make DESTDIR="$pkgdir" install
}

